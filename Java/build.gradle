apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = 'com.joseflavio'
archivesBaseName = 'transcriptograma'
version = '2015'

sourceCompatibility = 1.6
compileJava.options.encoding = 'ISO-8859-1'

sourceSets {
    main.java.srcDirs = ['fonte']
    main.resources.srcDirs = ['recurso']
}

task jarDist(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Transcriptograma',
                   'Implementation-Version': version,
                   'Main-Class': ''
    }
    from { configurations.compile.collect{ it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task jarFonte(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task jarJavadoc(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    javadoc.options.encoding = 'ISO-8859-1'
    javadoc.failOnError = false
}

artifacts {
    archives jarFonte, jarJavadoc
}

repositories {
    mavenCentral()
    flatDir {
       dirs 'biblioteca'
   }
}

dependencies {
    compile 'com.lyncode:jtwig-core:3.1.1'
}

eclipse {
    project {
        name = 'transcriptograma'
    }
    classpath {
        defaultOutputDir = file('binario')
        file {
            beforeMerged {
                classpath -> classpath.entries.removeAll { entry -> entry.kind == 'output' }
            }
        }
    }
}
