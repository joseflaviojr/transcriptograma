//------------------------------------------------------

apply plugin: "java"
apply plugin: "maven"
apply plugin: "eclipse"

//------------------------------------------------------

String nome = "Transcriptograma"
String descricao = "Ferramentas para construÃ§Ã£o e estudo de transcriptogramas."

group = "com.joseflavio"
archivesBaseName = "transcriptograma"
version = "2015"

//------------------------------------------------------

float javaVersao = 1.6
String codificacao = "ISO-8859-1"

sourceCompatibility = javaVersao
compileJava.options.encoding = codificacao

sourceSets {
    main.java.srcDirs = ["fonte"]
    main.resources.srcDirs = ["recurso"]
}

//------------------------------------------------------

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir:"biblioteca", include:["*.jar"])
    compile "com.lyncode:jtwig-core:3.1.1"
    compile "org.slf4j:slf4j-nop:1.7.12"
}

//------------------------------------------------------

task jarDist(type: Jar) {

    manifest {
        attributes "Implementation-Title": nome,
                   "Implementation-Version": version,
                   "Main-Class": ""
    }

    from { configurations.compile.collect{ it.isDirectory() ? it : zipTree(it) } }
    with jar
    
}

//------------------------------------------------------

task jarFonte(type: Jar) {
    
    classifier = "fonte"
    from sourceSets.main.allSource

    manifest {
        attributes "Implementation-Title": nome + " - Codigo Fonte",
                   "Implementation-Version": version,
                   "Main-Class": ""
    }

}

//------------------------------------------------------

task jarDoc(type: Jar) {

    classifier = "doc"
    from javadoc

    javadoc.options.encoding = codificacao
    javadoc.failOnError = false
    javadoc.title = nome + " " + version
    javadoc.options.links = []
    
}

//------------------------------------------------------

build.dependsOn jarDist

//------------------------------------------------------

artifacts {
    archives jarFonte, jarDoc
}

//------------------------------------------------------

eclipse {
    project {
        name = archivesBaseName
        comment = descricao
    }
    jdt {
        sourceCompatibility = javaVersao
        targetCompatibility = javaVersao
    }
    classpath {
        defaultOutputDir = file("binario")
        file {
            beforeMerged {
                classpath -> classpath.entries.removeAll { entry -> entry.kind == "output" }
            }
        }
    }
}

//------------------------------------------------------